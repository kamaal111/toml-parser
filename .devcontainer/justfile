# List available commands
default:
    just --list --unsorted

# Set up dev container. This step runs after building the dev container
post-create: setup-zsh-environment setup-zls install-atuin

[private]
setup-zls:
    #!/bin/zsh

    zig_version=$(zig version)

    if [ ! -d zls ]
    then
        git clone https://github.com/zigtools/zls
    fi

    cd zls
    git fetch
    git checkout $zig_version

    # builds to `zls/zig-out/bin/zls`
    zig build -Doptimize=ReleaseSafe

    cd ..

[private]
setup-zsh-environment:
    #!/bin/zsh

    echo "Updating zsh configuration"
    cp -f .zshrc ~/.zshrc

[private]
install-atuin:
    #!/bin/zsh

    echo Y | bash <(curl --proto '=https' --tlsv1.2 -sSf https://setup.atuin.sh)
